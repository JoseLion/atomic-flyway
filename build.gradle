buildscript {
  configurations.classpath {
    resolutionStrategy.activateDependencyLocking()
  }
}

plugins {
  id 'checkstyle'
  id 'java-library'
  id 'maven-publish'
  id 'signing'

  id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
  id 'io.github.joselion.pretty-jupiter' version '2.0.1'
  id 'io.github.joselion.strict-null-check' version '2.0.0'
  id 'se.solrike.sonarlint' version '1.0.0-beta.4'
}

def suffix = project.hasProperty('releaseVersion') ? '' : '-SNAPSHOT'

group = 'io.github.joselion'
version = "1.0.0$suffix"

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(18)
    vendor = JvmVendorSpec.ORACLE
  }

  withJavadocJar()
  withSourcesJar()
}

javadoc {
  options.addBooleanOption('html5', true)

  title = 'Atomic Flyway'
  options {
    addStringOption('Xwerror', '-quiet')
    tags('apiNote')
  }
}

jar {
  from sourceSets.main.allSource
  manifest {
    attributes(
      'Implementation-Title': project.name,
      'Implementation-Version': project.version
    )
  }
}

dependencyLocking {
  lockAllConfigurations()
}

checkstyle {
  setToolVersion('10.2')
}

sonarlint {
  excludeRules = [
    'java:S4032', // Allow packages only containing `package-info.java`
    'java:S107' // Allow constructors with more than 7 parameters
  ]
  includeRules = [
    'java:S4266' // "Stream.collect()" calls should not be redundant
  ]
}

repositories {
  mavenCentral()
}

def flywayVersion = '8.+'
def log4jVersion = '2.17.2'
def reactorVersion = '3.4.18'

dependencies {
  compileOnly("org.flywaydb:flyway-core:$flywayVersion")

  implementation('com.github.joselion:maybe:2.1.0')
  implementation('info.picocli:picocli:4.6.3')
  implementation("io.projectreactor:reactor-core:$reactorVersion")
  implementation("org.apache.logging.log4j:log4j-api:$log4jVersion")
  implementation("org.apache.logging.log4j:log4j-core:$log4jVersion")

  testImplementation('com.h2database:h2:2.1.212')
  testImplementation("io.projectreactor:reactor-test:$reactorVersion")
  testImplementation('org.assertj:assertj-core:3.22.0')
  testImplementation("org.flywaydb:flyway-core:$flywayVersion")
  testImplementation('uk.org.webcompere:system-stubs-core:2.0.1')
  testImplementation('uk.org.webcompere:system-stubs-jupiter:2.0.1')
}

testing {
  suites {
    test {
      useJUnitJupiter('5.8.2')

      targets {
        all {
          testTask.configure {
            systemProperty("java.security.manager", "allow")
          }
        }
      }
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from(components.java)

      pom {
        name = 'atomic-flyway'
        packaging = 'jar'
        description = """
          A wrapper around Flyway to make your migration atomic. That is to say, \
          it provides a revert mechanism that is set in place by design every \
          time you write a migration.
          """
        url = 'https://github.com/JoseLion/atomic-flyway'

        scm {
          connection = 'scm:git:https://github.com/JoseLion/atomic-flyway.git'
          developerConnection = 'scm:git:ssh://github.com/JoseLion/atomic-flyway.git'
          url = 'https://github.com/joselion/atomic-flyway'
        }

        licenses {
          license {
            name = 'Apache License 2.0'
            url = 'https://github.com/JoseLion/atomic-flyway/blob/main/LICENSE'
          }
        }

        developers {
          developer {
            id = 'JoseLion'
            name = "Jose Luis Leon"
            email = 'joseluis5000l@gmail.com'
          }
        }
      }
    }
  }
}

signing {
  sign(publishing.publications.mavenJava)
}

nexusPublishing {
  repositories {
    sonatype {
      nexusUrl.set(uri('https://s01.oss.sonatype.org/service/local/'))
      snapshotRepositoryUrl.set(uri('https://s01.oss.sonatype.org/content/repositories/snapshots/'))
    }
  }
}
